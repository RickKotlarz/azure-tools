// Unhealthy Azure Arc Connected Machines
resources
| where type =~ 'microsoft.hybridcompute/machines'
| extend computerName = properties.osProfile.computerName, lastStatusChange = properties.lastStatusChange, status = properties.status, os = properties.osType, cloudprovider = properties.detectedProperties.cloudprovider, machineFqdn = properties.machineFqdn, hostName = properties.hostName, dnsFqdn = properties.dnsFqdn, osSku = properties.osSku, errorDetails = properties.errorDetails, extensionServiceStatus = properties.serviceStatuses.extensionService.status, guestConfigurationServiceStatus = properties.serviceStatuses.guestConfigurationService.status, agentVersion = properties.agentVersion
| extend awsInstanceId = properties['detectedProperties']['AWS-instanceId']
| extend awsAccountId = properties['detectedProperties']['AWS-accountId']
| extend gcpinstanceId = properties['detectedProperties']['GCP-instanceId']
| extend gcpProject = properties['detectedProperties']['GCP-projectNumber']
| where status == 'Disconnected' or extensionServiceStatus !in ('active', 'running')  or guestConfigurationServiceStatus !in ('active', 'running')
| project hostName, machineFqdn, dnsFqdn, agentVersion, cloudprovider, subscriptionId, resourceGroup, status, lastStatusChange, os, osSku, extensionServiceStatus, guestConfigurationServiceStatus, errorDetails, awsInstanceId, awsAccountId, gcpinstanceId, gcpProject
